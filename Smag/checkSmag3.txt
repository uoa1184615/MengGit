
 on div; off allfac; on revpri; %on gcd; % avoid gcd unless necessary
 factor uu,h,ct,gx,gz,gam,r2,vv,qq,rqq;
 
 operator h; operator uu; operator vv;
 eta:=h(0,0); etax:=h(1,0)*d$ etay:=h(0,1)*d$ 
 depend h,xx,yy,tt; 
 depend uu,xx,yy,tt;
 depend vv,xx,yy,tt;
 let{ df(h(~m,~n),xx)=>h(m+1,n)
    , df(h(~m,~n),yy)=>h(m,n+1)
    , df(h(~m,~n),tt)=>df(gh,xx,m,yy,n)
    , df(uu(~m,~n),xx)=>uu(m+1,n)
    , df(uu(~m,~n),yy)=>uu(m,n+1)
    , df(uu(~m,~n),tt)=>df(gu,xx,m,yy,n)
    , df(vv(~m,~n),xx)=>vv(m+1,n)
    , df(vv(~m,~n),yy)=>vv(m,n+1)
    , df(vv(~m,~n),tt)=>df(gv,xx,m,yy,n)   
  };
 depend xx,x,y,z,t;
 depend yy,x,y,z,t;
 depend zz,x,y,z,t;
 depend tt,x,y,z,t;
 let { df(~a,x) => df(a,xx)*d-zz*etax/eta*df(a,zz)
     , df(~a,y) => df(a,yy)*d-zz*etay/eta*df(a,zz)
     , df(~a,t) => df(a,tt)-zz*gh/eta*df(a,zz)
     , df(~a,z) => df(a,zz)/eta 
     };
    
 depend qq,xx,yy,tt; %%rqq=1/qq;
 depend rqq,qq;
 let{ df(rqq,~aa)=>-rqq^2*df(qq,aa)
    , rqq*qq=>1
    , vv(0,0)^2*rqq=>qq-uu(0,0)^2*rqq
    , qq^2=>(uu(0,0)^2+vv(0,0)^2)
    , df(qq,~aa)=>(uu(0,0)*df(uu(0,0),aa)+vv(0,0)*df(vv(0,0),aa))*rqq
    };

 
  d:=eps; 
  grz:=gz; 
  grx:=eps*gx;
  gry:=0;
  gamm:=eps*gam; 
  let eps^3=>0;
  let r2^2=>2; 
  r2:=sqrt(2)$

  in "smag3Asym.txt"$
  
  exx:=df(u,x)$
  eyy:=df(v,y)$
  ezz:=df(w,z)$
  exz:=(df(u,z)+df(w,x))/2$
  exy:=(df(u,y)+df(v,x))/2$
  eyz:=(df(v,z)+df(w,y))/2$
  rese:=exx^2+ezz^2+eyy^2+2*exz^2+2*exy^2+2*eyz^2-ros^2;
  txx:=2*ct*eta^2*ros*exx$
  tyy:=2*ct*eta^2*ros*eyy$
  tzz:=2*ct*eta^2*ros*ezz$
  txz:=2*ct*eta^2*ros*exz$
  txy:=2*ct*eta^2*ros*exy$
  tyz:=2*ct*eta^2*ros*eyz$

  resw:=df(w,t)+u*df(w,x)+v*df(w,y)+w*df(w,z)+df(p,z)+grz-df(txz,x)
         -df(txy,y)-df(tzz,z);
  resu:=df(u,t)+u*df(u,x)+v*df(u,y)+w*df(u,z)+df(p,x)-grx
         -df(txx,x)-df(txy,y)-df(txz,z);
  resv:=df(v,t)+u*df(v,x)+v*df(v,y)+w*df(v,z)+df(p,y)-gry
         -df(tyy,y)-df(txy,x)-df(tyz,z);
  restn:=sub(zz=1,-p*(1+etax^2+etay^2)+tzz-2*etax*txz-2*etay*tyz+etax^2*txx
         +2*etax*etay*txy+etay^2*tyy);
  resc:=df(u,x)+df(v,y)+df(w,z);
  resgh:=sub(zz=1,w-u*etax-v*etay)-gh;
  resbu:=sub(zz=0,-u+cu*eta*df(u,z));
  resbv:=sub(zz=0,-v+cu*eta*df(v,z));
  resttu:=-sub(zz=1,(1-0*gamm)*((1-etax^2)*txz+etax*(tzz-txx)
          -etay*(txy+etax*tyz))-(1-gamm)*r2*ct/(cu+1)/(2*cu+1)*u*qq);
  resttv:=-sub(zz=1, (1-0*gamm)*((1-etay^2)*tyz+etay*(tzz-tyy)
          -etax*(txy+etay*txz))-(1-gamm)*r2*ct/(cu+1)/(2*cu+1)*v*qq);
  operator mean; linear mean; 
  let { mean(zz^~~n,zz) => 1/(n+1)
      , mean(1,zz) => 1 }; 
  mean_resu:=mean(resu,zz);
  mean_resv:=mean(resv,zz);
  mean_resw:=mean(resw,zz);

end;

