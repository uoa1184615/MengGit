 o:=3; % order of error
 on div; off allfac; on revpri; 
 factor uu,h,ct,gx,gz,gam,r2,vv,qq,rqq;
 
 operator h; operator uu; operator vv;
 eta:=h(0,0); etax:=h(1,0)*d$ etay:=h(0,1)*d$ 
 depend h,xx,yy,tt; 
 depend uu,xx,yy,tt;
 depend vv,xx,yy,tt;
 let{ df(h(~m,~n),xx)=>h(m+1,n)
    , df(h(~m,~n),yy)=>h(m,n+1)
    , df(h(~m,~n),tt)=>df(gh,xx,m,yy,n)
    , df(uu(~m,~n),xx)=>uu(m+1,n)
    , df(uu(~m,~n),yy)=>uu(m,n+1)
    , df(uu(~m,~n),tt)=>df(gu,xx,m,yy,n)
    , df(vv(~m,~n),xx)=>vv(m+1,n)
    , df(vv(~m,~n),yy)=>vv(m,n+1)
    , df(vv(~m,~n),tt)=>df(gv,xx,m,yy,n)   
  };
 depend xx,x,y,z,t;
 depend yy,x,y,z,t;
 depend zz,x,y,z,t;
 depend tt,x,y,z,t;
 let { df(~a,x) => df(a,xx)*d-zz*etax/eta*df(a,zz)
     , df(~a,y) => df(a,yy)*d-zz*etay/eta*df(a,zz)
     , df(~a,t) => df(a,tt)-zz*gh/eta*df(a,zz)
     , df(~a,z) => df(a,zz)/eta 
     };
    
 depend qq,xx,yy,tt; %%rqq=1/qq;
 depend rqq,qq;
 let{ df(rqq,~aa)=>-rqq^2*df(qq,aa)
    , rqq*qq=>1
    , vv(0,0)^2*rqq=>qq-uu(0,0)^2*rqq
    , qq^2=>(uu(0,0)^2+vv(0,0)^2)
    , df(qq,~aa)=>(uu(0,0)*df(uu(0,0),aa)+vv(0,0)*df(vv(0,0),aa))*rqq
    };
 
 
 operator wsolv; linear wsolv; 
 let { wsolv(zz^~~n,zz) => zz^(n+1)/(n+1)
     , wsolv(1,zz) => zz };
 operator psolv; linear psolv;
 let { psolv(zz^~~n,zz) => (1-zz^(n+1))/(n+1)
     , psolv(1,zz) => (1-zz) }; 
 operator mean; linear mean; 
 let { mean(zz^~~n,zz) => 1/(n+1)
     , mean(1,zz) => 1 }; 
 operator usolv; linear usolv;
 let { usolv(zz^~~n,zz) => (zz^(n+2)-(cu+zz)/(n+3)/(cu+1/2) )/(n+2)/(n+1)
     , usolv(1,zz) => (zz^2 -(cu+zz)/3/(cu+1/2) )/2 };

 let r2^2=>2; 
 r2=sqrt(2);
 operator uk,vk,wk,pk,ghk,guk,gvk,rosk; 
  
 u:=uu(0,0)*(cu+zz)/(cu+1/2)+for k:=1:o sum eps^k*uk(k); 
 v:=vv(0,0)*(cu+zz)/(cu+1/2)+for k:=1:o sum eps^k*vk(k);
 w:=+for k:=1:o sum eps^k*wk(k); 
 p:=grz*h(0,0)*(1-zz)+for k:=1:o sum eps^k*pk(k);  
 gh:=+for k:=1:o sum eps^k*ghk(k); 
 gu:=+for k:=1:o sum eps^k*guk(k);
 gv:=+for k:=1:o sum eps^k*gvk(k);
 ros:=qq*r2/eta/(1+2*cu)+for k:=1:o sum eps^k*rosk(k);

 depend uk,zz,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend vk,zz,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend wk,zz,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend pk,zz,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend ghk,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend guk,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend gvk,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 depend rosk,zz,xx,yy,uu(0,0),vv(0,0),h(0,0),
        uu(1,0),uu(0,1),vv(1,0),vv(0,1),h(1,0),h(0,1),
        uu(1,1),vv(1,1),h(1,1),
        uu(2,0),uu(0,2),vv(2,0),vv(0,2),h(2,0),h(0,2);
 let { df(uk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(uk(k),uu(m,n))*df(uu(m,n),tt)
       +df(uk(k),vv(m,n))*df(vv(m,n),tt)
       +df(uk(k),h(m,n))*df(h(m,n),tt))
      ,df(vk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(vk(k),uu(m,n))*df(uu(m,n),tt)
       +df(vk(k),vv(m,n))*df(vv(m,n),tt)
       +df(vk(k),h(m,n))*df(h(m,n),tt))
      ,df(wk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(wk(k),uu(m,n))*df(uu(m,n),tt)
       +df(wk(k),vv(m,n))*df(vv(m,n),tt)
       +df(wk(k),h(m,n))*df(h(m,n),tt))
      ,df(pk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(pk(k),uu(m,n))*df(uu(m,n),tt)
       +df(pk(k),vv(m,n))*df(vv(m,n),tt)
       +df(pk(k),h(m,n))*df(h(m,n),tt))
      ,df(ghk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(ghk(k),uu(m,n))*df(uu(m,n),tt)
       +df(ghk(k),vv(m,n))*df(vv(m,n),tt)
       +df(ghk(k),h(m,n))*df(h(m,n),tt))
      ,df(guk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(guk(k),uu(m,n))*df(uu(m,n),tt)
       +df(guk(k),vv(m,n))*df(vv(m,n),tt)
       +df(guk(k),h(m,n))*df(h(m,n),tt))
      ,df(gvk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(gvk(k),uu(m,n))*df(uu(m,n),tt)
       +df(gvk(k),vv(m,n))*df(vv(m,n),tt)
       +df(gvk(k),h(m,n))*df(h(m,n),tt))
      ,df(rosk(~k),tt)=>for m:=0:k sum for n:=0:k-m sum
       (df(rosk(k),uu(m,n))*df(uu(m,n),tt)
       +df(rosk(k),vv(m,n))*df(vv(m,n),tt)
       +df(rosk(k),h(m,n))*df(h(m,n),tt))
     };


 
  ct:=1/50; 
  cu:=11/6;
  d:=eps; 
  grz:=gz; 
  grx:=eps*gx;
  gry:=0;
  gamm:=eps*gam; 
  factor eps; 
  for i:=o:o do let eps^i=>0;
  
  resc:=df(u,x)+df(v,y)+df(w,z);
  resgh:=sub(zz=1,w-u*etax-v*etay)-gh;
    
 exx:=df(u,x);
 eyy:=df(v,y);
 ezz:=df(w,z);
 exz:=(df(u,z)+df(w,x))/2;
 exy:=(df(u,y)+df(v,x))/2;
 eyz:=(df(v,z)+df(w,y))/2;
 rese:=exx^2+ezz^2+eyy^2+2*exz^2+2*exy^2+2*eyz^2-ros^2;
 txx:=2*ct*eta^2*ros*exx;
 tyy:=2*ct*eta^2*ros*eyy;
 tzz:=2*ct*eta^2*ros*ezz;
 txz:=2*ct*eta^2*ros*exz;
 txy:=2*ct*eta^2*ros*exy;
 tyz:=2*ct*eta^2*ros*eyz;
  
 resw:=df(w,t)+u*df(w,x)+v*df(w,y)+w*df(w,z)+df(p,z)+grz-df(txz,x)
         -df(txy,y)-df(tzz,z);
 restn:=sub(zz=1,-p*(1+etax^2+etay^2)+tzz-2*etax*txz-2*etay*tyz+etax^2*txx
         +2*etax*etay*txy+etay^2*tyy);

 resu:=df(u,t)+u*df(u,x)+v*df(u,y)+w*df(u,z)+df(p,x)-grx
         -df(txx,x)-df(txy,y)-df(txz,z);
 resv:=df(v,t)+u*df(v,x)+v*df(v,y)+w*df(v,z)+df(p,y)-gry
         -df(tyy,y)-df(txy,x)-df(tyz,z);
 resbu:=sub(zz=0,-u+cu*eta*df(u,z));
 resbv:=sub(zz=0,-v+cu*eta*df(v,z));

 resttu:=-sub(zz=1,(1-0*gamm)*((1-etax^2)*txz+etax*(tzz-txx)
          -etay*(txy+etax*tyz))-(1-gamm)*r2*ct/(cu+1)/(2*cu+1)*u*qq);
 resttv:=-sub(zz=1, (1-0*gamm)*((1-etay^2)*tyz+etay*(tzz-tyy)
          -etax*(txy+etay*txz))-(1-gamm)*r2*ct/(cu+1)/(2*cu+1)*v*qq);
 
  resc:=rest(coeff(resc,eps)); 
  resgh:=rest(coeff(resgh,eps)); 
  resw:=rest(coeff(resw,eps)); 
  restn:=rest(coeff(restn,eps)); 
  rese:=rest(coeff(rese,eps)); 
  resu:=rest(coeff(resu,eps));
  resv:=rest(coeff(resv,eps)); 
  resttu:=rest(coeff(resttu,eps)); 
  resttv:=rest(coeff(resttv,eps));
  resbu:=rest(coeff(resbu,eps));
  resbv:=rest(coeff(resbv,eps));
  
  array aw(o),agh(o),ap(o),au(o),av(o),aros(o),agu(o),agv(o);
  for k:=1:o-1 do begin 
  write "ORDER = ",k;
  
  res1:=sub(wk(k)=0,part(resc,k)); 
  aw(k):=-eta*wsolv(res1,zz); 
  let wk(k)=>aw(k);
  write err:=part(resc,k);
  
  agh(k):=sub({ghk(k)=0,zz=1},part(resgh,k)); 
  let ghk(k)=>agh(k);
  write err:=sub(zz=1,part(resgh,k));
  
  res1:=sub(pk(k)=0,part(resw,k)); 
  res2:=sub({pk(k)=0,zz=1},part(restn,k)); 
  ap(k):=res2+eta*psolv(res1,zz); 
  let pk(k)=>ap(k); 
  write err:={part(resw,k),sub(zz=1,part(restn,k))};
   
  ros1:=sub(rosk(k)=0,part(rese,k))*(cu+1/2)/r2*eta*rqq;

  res1:=sub({uk(k)=0,vk(k)=0},sub(rosk(k)=ros1,part(resu,k)));
  res2:=sub({uk(k)=0,vk(k)=0},sub(rosk(k)=ros1,part(resv,k)));
  au(k):=eta*rqq^3*(cu+1/2)/ct/r2*usolv(
         (uu(0,0)^2+2*vv(0,0)^2)*res1
         -uu(0,0)*vv(0,0)*res2  ,zz);
  av(k):=eta*rqq^3*(cu+1/2)/ct/r2*usolv(
         (vv(0,0)^2+2*uu(0,0)^2)*res2
         -uu(0,0)*vv(0,0)*res1  ,zz);

  let { uk(k)=>au(k)
      , vk(k)=>av(k)};
  aros(k):=ros1;
  let rosk(k)=>aros(k); 
  write err:={part(resu,k),part(resv,k),part(rese,k)
        ,sub(zz=0,part(resbu,k)),sub(zz=0,part(resbv,k))
        ,mean(au(k),zz),mean(av(k),zz)}; 

  res1:=part(resttu,k); res1:=sub(zz=1,res1);
  res2:=part(resttv,k); res2:=sub(zz=1,res2);
  res1:=sub({guk(k)=0,gvk(k)=0},res1);
  res2:=sub({guk(k)=0,gvk(k)=0},res2);
  agu(k):=-3*(1+2*cu)*(1+cu)
      /2/eta/(3+11*cu+12*cu^2)/(1+3*cu+3*cu^2)/(3+4*cu)
      *(((1+5*cu+8*cu^2)*uu(0,0)^2*rqq^2-(9+45*cu+80*cu^2+48*cu^3))*res1
        +(1+5*cu+8*cu^2)*uu(0,0)*vv(0,0)*rqq^2*res2);
  agv(k):=-3*(1+2*cu)*(1+cu)
      /2/eta/(3+11*cu+12*cu^2)/(1+3*cu+3*cu^2)/(3+4*cu)
      *(((1+5*cu+8*cu^2)*vv(0,0)^2*rqq^2-(9+45*cu+80*cu^2+48*cu^3))*res2
        +(1+5*cu+8*cu^2)*uu(0,0)*vv(0,0)*rqq^2*res1);
 let { guk(k)=>agu(k)
      , gvk(k)=>agv(k)}; 
  res1:=part(resttu,k)$ res2:=part(resttv,k)$
  write err:={sub(zz=1,res1),sub(zz=1,res2)};  

  
end; 
  
out "smag3Asym.txt"$ off nat;
   r2:=sqrt(2)$
  % on rounded; print_precision 5;
   cu:=cu;
   ct:=ct;
   gh:=gh;
   gu:=gu;
   gv:=gv;
   p:=p;
   w:=w;
   u:=u;
   v:=v;
   ros:=ros;
   write "end;";
shut "smag3Asym.txt"$ on nat;
  showtime;
  end;
  
  