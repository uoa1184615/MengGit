% MC 1 June 2012
on div; off allfac; on revpri; on gcd; on time;
linelength 60;
depend xi,x; let df(xi,x)=>1/dd;
operator cis; 
let {cis(0)=>1, cis(~a)^2=>cis(2*a),
      cis(~a)*cis(~b)=>cis(a+b), 
      df(cis(~a),~b)=>i*cis(a)*df(a,b) };
ee:=cis(lam*t+k*j); 
ff:=cis(-lam*t-k*j);

%%% define the forms of the solutions
nu:=nu0+nu2*ll^2/dd^2;
u0:= ( a0*cis(ll*xi)+b0*cis(-ll*xi))*ee;
h0:= (-(lam-i*nu)*dd/ll*a0*cis(ll*xi)+(lam-i*nu)*dd/ll*b0*cis(-ll*xi))*ee; 
u1:= ( a1*cis(ll*xi)+b1*cis(-ll*xi))*ee;
h1:=(-(lam-i*nu)*dd/ll*a1*cis(ll*xi)+(lam-i*nu)*dd/ll*b1*cis(-ll*xi))*ee;
%%% the coupled PDEs, the solutions confirms the pdes equal to zeros
let lam^2=>ll^2/dd^2+i*nu*lam;
pde:={(df(u0,t)+df(h0,x)+nu0*u0-nu2*df(u0,x,2))
    ,(df(h0,t)+df(u0,x))
    ,(df(u1,t)+df(h1,x)+nu0*u1-nu2*df(u1,x,2))
    ,(df(h1,t)+df(u1,x))};

%%% use gam control the output orders
factor i;
let gam^4=>0;

%%% the coupling conditions with the errors O(gam^4)
eehp1:=sub({j=j+1,xi=0},h1)$
eehp3:=sub({j=j+3,xi=0},h1)$
eehp5:=sub({j=j+5,xi=0},h1)$
eehm1:=sub({j=j-1,xi=0},h1)$
eehm3:=sub({j=j-3,xi=0},h1)$
eehm5:=sub({j=j-5,xi=0},h1)$
eeup1:=sub({j=j+1,xi=0},u0)$
eeup3:=sub({j=j+3,xi=0},u0)$
eeup5:=sub({j=j+5,xi=0},u0)$
eeum1:=sub({j=j-1,xi=0},u0)$
eeum3:=sub({j=j-3,xi=0},u0)$
eeum5:=sub({j=j-5,xi=0},u0)$

%% the following are errors O(gam^4)
chl:=(-sub(xi=+r,h0)
     +gam*(eehp1+eehm1)/2
     +gam*r/2*(eehp1-eehm1)
     +gam^2*(-1+r^2)/16*(eehp3-eehp1-eehm1+eehm3)
     +gam^2*(-r+r^3)/48*(eehp3-3*eehp1+3*eehm1-eehm3)
     +gam^3*(9-10*r^2+r^4)/768*(eehp5+eehp3-2*eehp1
       -2*eehm1+eehm3+eehm5)
     +gam^3*(9*r-10*r^3+r^5)/3840*(eehp5-5*eehp3+10*eehp1
       -10*eehm1+5*eehm3-eehm5) )*ff$
chr:=(-sub(xi=-r,h0)
     +gam*(eehp1+eehm1)/2
     -gam*r/2*(eehp1-eehm1)
     +gam^2*(-1+r^2)/16*(eehp3-eehp1-eehm1+eehm3)
     -gam^2*(-r+r^3)/48*(eehp3-3*eehp1+3*eehm1-eehm3)
     +gam^3*(9-10*r^2+r^4)/768*(eehp5+eehp3-2*eehp1
       -2*eehm1+eehm3+eehm5)
     -gam^3*(9*r-10*r^3+r^5)/3840*(eehp5-5*eehp3+10*eehp1
       -10*eehm1+5*eehm3-eehm5) )*ff$
cul:=(-sub(xi=+r,u1)
     +gam*(eeup1+eeum1)/2
     +gam*r/2*(eeup1-eeum1)
     +gam^2*(-1+r^2)/16*(eeup3-eeup1-eeum1+eeum3)
     +gam^2*(-r+r^3)/48*(eeup3-3*eeup1+3*eeum1-eeum3)
     +gam^3*(3-10*r^2+r^4)/768*(eeup5+eeup3-2*eeup1
       -2*eeum1+eeum3+eeum5) 
     +gam^3*(9*r-10*r^3+r^5)/3840*(eeup5-5*eeup3+10*eeup1
       -10*eeum1+5*eeum3-eeum5) )*ff$  
cur:=(-sub(xi=-r,u1)
     +gam*(eeup1+eeum1)/2
     -gam*r/2*(eeup1-eeum1)
     +gam^2*(-1+r^2)/16*(eeup3-eeup1-eeum1+eeum3)
     -gam^2*(-r+r^3)/48*(eeup3-3*eeup1+3*eeum1-eeum3)
     +gam^3*(3-10*r^2+r^4)/768*(eeup5+eeup3-2*eeup1
       -2*eeum1+eeum3+eeum5) 
     -gam^3*(9*r-10*r^3+r^5)/3840*(eeup5-5*eeup3+10*eeup1
       -10*eeum1+5*eeum3-eeum5) )*ff$
%%% solutions of characteristic equations     
a:=mat((df(chl,a0),df(chl,b0),df(chl,a1),df(chl,b1)) 
       ,(df(chr,a0),df(chr,b0),df(chr,a1),df(chr,b1)) 
       ,(df(cul,a0),df(cul,b0),df(cul,a1),df(cul,b1)) 
       ,(df(cur,a0),df(cur,b0),df(cur,a1),df(cur,b1)) )$

%write "determining";      
chareqn:=(det(a) where cis(~q)=>cos(q)+i*sin(q))$
%write "trigsimping";
chareqn:=trigsimp(chareqn,expand)$
%write "factorizing";
cden:=factorize(den(chareqn));
cnum:=factorize(num(chareqn));
pde:=pde;

end;
